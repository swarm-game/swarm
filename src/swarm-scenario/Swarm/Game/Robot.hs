{-# LANGUAGE TypeFamilies #-}
{-# LANGUAGE UndecidableInstances #-}

-- |
-- SPDX-License-Identifier: BSD-3-Clause
--
-- A data type to represent robots.
--
-- The lenses exported from this module are all specialized to @Robot
-- Instantiated@ for convenience.  If you need more polymorphic
-- lenses, see "Swarm.Game.Robot.Generic".  See Note [Robot lens
-- types].
module Swarm.Game.Robot (
  -- * Robots data
  _robotID,
  _robotLocation,
  RobotMachine,
  _machine,
  RobotActivity,
  _activityCounts,
  RobotLogMember,
  _robotLog,
  RobotLogUpdatedMember,
  _robotLogUpdated,

  -- * Robots
  RID,
  Robot,

  -- ** Lenses
  robotEntity,
  robotName,
  unwalkableEntities,
  robotCreatedAt,
  robotDisplay,
  robotLocation,
  unsafeSetRobotLocation,
  trobotLocation,
  robotOrientation,
  robotInventory,
  equippedDevices,
  inventoryHash,
  robotCapabilities,
  walkabilityContext,
  robotID,
  robotParentID,
  robotHeavy,
  systemRobot,
  selfDestruct,
  runningAtomic,

  -- ** Creation & instantiation
  mkRobot,

  -- ** Query
  robotKnows,
  isInteractive,

  -- ** Constants
  hearingDistance,

  -- ** Rendering
  renderRobot,
) where

import Control.Lens hiding (Const, contains)
import Data.Hashable (hashWithSalt)
import Data.Text (Text)
import Swarm.Game.Cosmetic.Color (AttributeMap, TrueColor)
import Swarm.Game.Cosmetic.Display (Display, invisible)
import Swarm.Game.Cosmetic.Texel (Texel)
import Swarm.Game.Entity hiding (empty)
import Swarm.Game.Location (Heading, Location)
import Swarm.Game.Robot.Generic (
  RID,
  Robot (_activityCounts, _machine, _robotID, _robotLocation, _robotLog, _robotLogUpdated),
  RobotActivity,
  RobotLogMember,
  RobotLogUpdatedMember,
  RobotMachine,
  mkRobot,
  robotCapabilities,
  robotEntity,
  robotID,
  trobotLocation,
  unsafeSetRobotLocation,
 )
import Swarm.Game.Robot.Generic qualified as G
import Swarm.Game.Robot.Walk
import Swarm.Game.Universe
import Swarm.Language.Syntax (Phase (..))
import System.Clock (TimeSpec)

-- | Entities that the robot cannot move onto. See also 'walkabilityContext'.
unwalkableEntities :: Lens' (Robot Instantiated) (WalkabilityExceptions EntityName)
unwalkableEntities = G.unwalkableEntities

-- | The creation date of the robot.
robotCreatedAt :: Lens' (Robot Instantiated) TimeSpec
robotCreatedAt = G.robotCreatedAt

-- | The name of a robot.
robotName :: Lens' (Robot Instantiated) Text
robotName = G.robotName

-- | The 'Display' of a robot.
robotDisplay :: Lens' (Robot Instantiated) Display
robotDisplay = G.robotDisplay

-- | The robot's current location, represented as @(x,y)@.  This is only
--   a getter, since when changing a robot's location we must remember
--   to update the 'Swarm.Game.State.robotsByLocation' map as well.  You can use the
--   'Swarm.Game.Step.updateRobotLocation' function for this purpose.
robotLocation :: Getter (Robot Instantiated) (Cosmic Location)
robotLocation = G.robotLocation

-- | Which way the robot is currently facing.
robotOrientation :: Lens' (Robot Instantiated) (Maybe Heading)
robotOrientation = G.robotOrientation

-- | The robot's inventory.
robotInventory :: Lens' (Robot Instantiated) Inventory
robotInventory = G.robotInventory

-- | The ID number of the robot's parent, that is, the robot that
--   built (or most recently reprogrammed) this robot, if there is
--   one.
robotParentID :: Lens' (Robot Instantiated) (Maybe RID)
robotParentID = G.robotParentID

-- | Is this robot extra heavy (thus requiring tank treads to move)?
robotHeavy :: Lens' (Robot Instantiated) Bool
robotHeavy = G.robotHeavy

-- | A separate inventory for equipped devices, which provide the
--   robot with certain capabilities.
--
--   Note that every time the inventory of equipped devices is
--   modified, this lens recomputes a cached set of the capabilities
--   the equipped devices provide, to speed up subsequent lookups to
--   see whether the robot has a certain capability (see 'robotCapabilities')
--
--   This lens is actually used at multiple types; see Note [Robot
--   lens types].
equippedDevices :: Lens' (Robot Instantiated) Inventory
equippedDevices = G.equippedDevices

-- | A hash of a robot's entity record and equipped devices, to
--   facilitate quickly deciding whether we need to redraw the robot
--   info panel.
inventoryHash :: Getter (Robot Instantiated) Int
inventoryHash = to (\r -> 17 `hashWithSalt` (r ^. (robotEntity . entityHash)) `hashWithSalt` (r ^. equippedDevices))

-- | Does a robot know of an entity's existence?
robotKnows :: Robot Instantiated -> Entity -> Bool
robotKnows r e = contains0plus e (r ^. robotInventory) || contains0plus e (r ^. equippedDevices)

isInteractive :: Robot Instantiated -> Bool
isInteractive r = not $ r ^. robotDisplay . invisible && r ^. systemRobot

-- | Is this robot a "system robot"?  System robots are generated by
--   the system (as opposed to created by the user) and are not
--   subject to the usual capability restrictions.
systemRobot :: Lens' (Robot Instantiated) Bool
systemRobot = G.systemRobot

-- | Does this robot wish to self destruct?
selfDestruct :: Lens' (Robot Instantiated) Bool
selfDestruct = G.selfDestruct

-- | Is the robot currently running an atomic block?
runningAtomic :: Lens' (Robot Instantiated) Bool
runningAtomic = G.runningAtomic

-- | Put together a 'WalkabilityContext' for a robot.
walkabilityContext :: Getter (Robot Instantiated) WalkabilityContext
walkabilityContext = G.walkabilityContext

-- | Default distance at which a robot can hear things.
hearingDistance :: Num i => i
hearingDistance = 32

-- | Render a robot to a texel.
renderRobot :: AttributeMap -> Robot phase -> Texel TrueColor
renderRobot aMap r = renderEntity aMap (const False) True (r ^. robotEntity)
